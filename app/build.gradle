plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

android {
    compileSdk 33
    ndkVersion "25.2.9519653"

    defaultConfig {
        applicationId "moe.madoka.umapyogin_android"
        minSdk 21
        targetSdk 33
        versionCode 1
        versionName "1.3"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        externalNativeBuild {
            cmake {
                abiFilters 'arm64-v8a'
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    dataBinding {
        enabled = true
    }
    sourceSets {
        main {
            assets {
                srcDirs += ["build/intermediates/umapyogin_extra_assets"]
            }
        }
    }
}

dependencies {
    compileOnly 'de.robv.android.xposed:api:82'
    compileOnly 'de.robv.android.xposed:api:82:sources'
    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.8.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}

def translateDataDir = buildSC == "true" ? "Trainers-Legend-G-TRANS" : "Trainers-Legend-G-TRANS-zh-tw"

task copyStoryLocalizedData(type: Copy) {
    from("../$translateDataDir/localized_data/stories") {
        include("**/*.json")
    }
    into "build/intermediates/umapyogin_extra_assets/UmaPyoginFiles/stories"
}

task copyStaticLocalizedData(type: Copy) {
    from "../$translateDataDir/localized_data/static.json"
    into "build/intermediates/umapyogin_extra_assets/UmaPyoginFiles"
}

task copyDatabaseLocalizedData(type: Copy) {
    from("../$translateDataDir/localized_data") {
        include "text_data.json", "character_system_text.json", "race_jikkyo_comment.json", "race_jikkyo_message.json"
    }
    into "build/intermediates/umapyogin_extra_assets/UmaPyoginFiles/database"
}

task copyLocalizedData {
}

copyLocalizedData.dependsOn copyStoryLocalizedData
copyLocalizedData.dependsOn copyStaticLocalizedData
copyLocalizedData.dependsOn copyDatabaseLocalizedData

preBuild.dependsOn copyLocalizedData
