plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

android {
    compileSdk 31
    ndkVersion "24.0.8215888"

    defaultConfig {
        applicationId "moe.madoka.umapyogin_android"
        minSdk 21
        targetSdk 31
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        externalNativeBuild {
            cmake {
                abiFilters 'arm64-v8a'
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    dataBinding {
        enabled = true
    }
    sourceSets {
        main {
            assets {
                srcDirs += [ "build/intermediates/umapyogin_extra_assets" ]
            }
        }
    }
}

dependencies {
    compileOnly 'de.robv.android.xposed:api:82'
    compileOnly 'de.robv.android.xposed:api:82:sources'
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.6.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}

task copyStoryLocalizedData(type: Copy) {
    from("../Trainers-Legend-G-TRANS/localized_data/stories") {
        include("**/*.json")
    }
    into "build/intermediates/umapyogin_extra_assets/UmaPyoginFiles/stories"
}

task copyStaticLocalizedData(type: Copy) {
    from "../Trainers-Legend-G-TRANS/localized_data/static.json"
    into "build/intermediates/umapyogin_extra_assets/UmaPyoginFiles"
}

task copyHashLocalizedData(type: Copy) {
    from("../Trainers-Legend-G-TRANS/localized_data") {
        include("*.json")
        exclude("static.json")
    }
    into "build/intermediates/umapyogin_extra_assets/UmaPyoginFiles/hash_dicts"
}

task copyLocalizedData {
}

copyLocalizedData.dependsOn copyStoryLocalizedData
copyLocalizedData.dependsOn copyStaticLocalizedData
copyLocalizedData.dependsOn copyHashLocalizedData

preBuild.dependsOn copyLocalizedData
